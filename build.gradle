plugins {
    id 'com.github.arcticlampyrid.gradle-git-version' version '1.0.4'
    id 'info.solidsoft.pitest' version '1.7.4' apply false
    id 'net.ltgt.errorprone' version '2.0.2' apply false
}

allprojects { project ->
    group 'io.github.joke.objects'
    version rootProject.version

    repositories {
        mavenCentral()
    }

    pluginManager.withPlugin('base') {
        tasks.withType(JavaCompile) {
            options.release = 8
            options.compilerArgs << '-parameters' << '-Werror'
        }
    }

    pluginManager.withPlugin('jacoco') {
        test.finalizedBy jacocoTestReport
        jacocoTestReport {
            dependsOn test
            reports {
                xml.required = true
            }
        }
    }

    pluginManager.withPlugin('info.solidsoft.pitest') {
        pitest {
            outputFormats = ['XML', 'HTML']
            exportLineCoverage = true
            timestampedReports = false
            pitestVersion = '1.7.5'
            junit5PluginVersion = '0.15'
            mutators = ['ALL']
            historyInputLocation = "$buildDir/reports/pitest/history"
            historyOutputLocation = "$buildDir/reports/pitest/history"
        }
    }

    pluginManager.withPlugin('net.ltgt.errorprone') {
        tasks.withType(JavaCompile) {
            options.errorprone {
                disableWarningsInGeneratedCode = true
                error('NullAway')
                option('NullAway:AnnotatedPackages', project.group)
            }
        }
    }

}

wrapper {
    gradleVersion = '7.4.2'
}
